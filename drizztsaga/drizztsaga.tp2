/* -------------------------------------------------------------------------------------------------------------
  The Drizzt Saga by flysoup
  

 v1 initial WeiDU conversion and tp2 code by pro5
 v2 by Badgert and erebusant
 v3 BGEE update by pro5
 v4 Maintenance update & bug fixes
 
------------------------------------------------------------------------------------------------------------- */

/* Backup folder */
BACKUP ~DRIZZTSAGA/backup~

AUTHOR ~flysoup, http://forums.blackwyrmlair.net/index.php?showforum=103~
VERSION ~pre-v4.00~
README ~DRIZZTSAGA\ReadMe.htm~

ASK_EVERY_COMPONENT

// Actions executed for every component
ALWAYS
 
  // only do this once per Weidu session
  ACTION_IF NOT VARIABLE_IS_SET ~GameTypeDetected~ BEGIN
 	
	ACTION_IF GAME_IS ~BGEE~ BEGIN
		// Enhanced Edition:
		PRINT @1500
		INCLUDE ~DRIZZTSAGA\lib\g3_bgee_cpmvars.tpa~
	END
	ELSE
	ACTION_IF GAME_IS ~Tutu_TotSC~ BEGIN
		// Tutu:
		PRINT @353
		INCLUDE ~DRIZZTSAGA\lib\g3_tutu_cpmvars.tpa~
	END
	ELSE
	ACTION_IF GAME_IS ~BGT~ BEGIN
		// BGT:
		PRINT @354
		INCLUDE ~DRIZZTSAGA\lib\g3_bgt_cpmvars.tpa~
	END

  	// copy ASCII or UTF8 versions of TRAs depending on the platform + finalize sound references while we're at it
  	COPY + ~DRIZZTSAGA\Language\%LANGUAGE%\%SrcFolder%~ ~DRIZZTSAGA\Language\%LANGUAGE%~
  		EVALUATE_BUFFER
  	
  	// Load our finalized primary TRA
	LOAD_TRA ~DRIZZTSAGA\Language\%LANGUAGE%\main.tra~		

	// whitespace variable for use in REPLACE_TEXTUALLY
	OUTER_PATCH wlib BEGIN
		WRITE_LONG 0x0 0x090a0d20
		READ_ASCII 0x0 ws(4) //detects any whitespace
	END
	
	// Include macro definitions and other functions
	INCLUDE ~%MOD_FOLDER%\lib\worldmap_update.tph~
	INCLUDE ~%MOD_FOLDER%\lib\del_are_trigger.tph~
	INCLUDE ~%MOD_FOLDER%\tpa\ds-core.tpa~
	INCLUDE ~%MOD_FOLDER%\tpa\ds-nerfing.tpa~
	INCLUDE ~%MOD_FOLDER%\tpa\ds-bpwm.tpa~
		
	OUTER_SET GameTypeDetected = 1
		  	
  END  // IF NOT VARIABLE_IS_SET ~GameTypeDetected~

END  // ALWAYS


AUTO_TRA ~DRIZZTSAGA/Language/%s~

// (See readme for translation credits)
LANGUAGE ~English~								~english~	 ~DRIZZTSAGA/Language/English/setup.tra~
LANGUAGE ~French~								~french~	 ~DRIZZTSAGA/Language/French/setup.tra~
LANGUAGE ~Italian~								~italian~	 ~DRIZZTSAGA/Language/Italian/setup.tra~
LANGUAGE ~Polish~								~polish~	 ~DRIZZTSAGA/Language/Polish/setup.tra~	 
LANGUAGE ~Russian~								~russian~	 ~DRIZZTSAGA/Language/Russian/setup.tra~
LANGUAGE ~Spanish~								~spanish~	 ~DRIZZTSAGA/Language/Spanish/setup.tra~
LANGUAGE ~Simplified Chinese~								~schinese~	 ~DRIZZTSAGA/Language/schinese/setup.tra~

///////////////////////////////////////////////////////////////////////////////////////////////
//// Original version: new areas connected by travel triggers /////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @1502
DESIGNATED 0
SUBCOMPONENT @0
REQUIRE_PREDICATE GAME_IS ~BGEE BGT Tutu_TotSC~ @356

LAM DS_CORE_COMPONENT

// Worldmap

PRINT @359
LAM WORLDMAP_UPDATE

// BP-BGT Worldmap compatibility (yes, the default version will work in BWP install with Worldmap mod too):
LAF ADD_BP_BGT_WORLDMAP
	STR_VAR
		AreasFilename = "bpwm_areas.tbl"
		AreasForHugeFilename = "bpwm_areas_huge.tbl"
		LinksFilename = "bpwm_links_original.tbl"
		TraFilename = "bpwm_worldmap.tra"
	END

/////////////////////////////////////////////////////////////////////////////////////////////////////
//// BP-BGT Worldmap version: access all new areas through worldmap /////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @489
DESIGNATED 1
SUBCOMPONENT @0
REQUIRE_PREDICATE GAME_IS ~BGEE BGT Tutu_TotSC~ @356

LAM DS_CORE_COMPONENT

// Worldmap

PRINT @359

/*
Unnecessary, since BPWM will overwrite this stuff anyway and without BPWM this subcomponent won't work properly in any case because of the changes below
LAUNCH_ACTION_MACRO ~WORLDMAP_UPDATE~
*/

// Tell BP-BGT Worldmap what we need added to it:
LAF ADD_BP_BGT_WORLDMAP
	STR_VAR
		AreasFilename = "bpwm_areas.tbl"
		AreasForHugeFilename = "bpwm_areas_huge.tbl"
		LinksFilename = "bpwm_links_all.tbl"
		TraFilename = "bpwm_worldmap.tra"
	END

// One of new areas now revealed via script
EXTEND_TOP ~F_0113.BCS~ ~DRIZZTSAGA/Scripts/SNIP/eF_0113.BAF~

// For other areas, replace default travel triggers with searchmap WM regions
COPY_EXISTING ~F_0111.ARE~ ~override~
  SPRINT TRIGGERNAME "bear village"
  SET TRIGGERTYPE = 2
  LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~F_0112.ARE~ ~override~
  SPRINT TRIGGERNAME "GIANT HILLS"
  LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~F_7777.ARE~ ~override~
  SPRINT TRIGGERNAME "#icewind02"
  LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~F_7779.ARE~ ~override~
  SPRINT TRIGGERNAME "#icewind05"
  LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
  SPRINT TRIGGERNAME "#tocrate"
  LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~F_8888.ARE~ ~override~
  SPRINT TRIGGERNAME "#thecrate"
  LAUNCH_PATCH_MACRO ~DELETE_INFO_TRIGGER~
BUT_ONLY_IF_IT_CHANGES

COPY ~DRIZZTSAGA\ARE-DATA\BPWM~ ~override~


///////////////////////////////////////////////////////////////////
//// DEPRECATED: XP cap patching - now an optional component //////
///////////////////////////////////////////////////////////////////

BEGIN @1507  // Raise the XP cap
DESIGNATED 2
DEPRECATED @1515
REQUIRE_PREDICATE NOT GAME_IS ~BGT~ @1506
WARN @1515

///////////////////////////////////////////////////////////////////////////////////////////////
//// Delayed start: Drizzt joins after Durlag's Tower - an optional component /////////////////
///////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @1508
DESIGNATED 3
REQUIRE_PREDICATE MOD_IS_INSTALLED "drizztsaga.tp2" "0" OR MOD_IS_INSTALLED "drizztsaga.tp2" "1" @1503

<<<<<<<< .../DS-inlined/P5empty.baf
>>>>>>>>

// Installation marker for this component
// Can also be checked for from scripts by checking for Global("F_DSDelayedStart","GLOBAL",1)
COPY ~.../DS-inlined/P5empty.baf~ ~DRIZZTSAGA\markers\DelayedStart.xxx~

// Reverse replacing Drizzt's CRE file reference
COPY_EXISTING ~%FishermansLake%.ARE~ ~override~
	READ_LONG 0x54 ~actoroffset~
	READ_SHORT 0x58 ~actornum~
	SET ~numsofar~=0
	WHILE (~%numsofar%~<~%actornum%~) BEGIN
		READ_ASCII (~%numsofar%~*0x110 + ~%actoroffset%~+0x80) ~crename~ //(8)
		PATCH_IF ~%crename%~ STRING_EQUAL_CASE ~F_DRIZZT~ THEN BEGIN
			WRITE_ASCIIE (~%numsofar%~*0x110 + ~%actoroffset%~+0x80) ~%_%DRIZZT~ #8
			SET numsofar = actornum
		END
		SET ~numsofar~=~%numsofar%~+1
	END	
BUT_ONLY

// Reverse changes to Drizzt's DV in gnoll scripts:
COPY_EXISTING ~%_%GNOLLDR.BCS~ ~override~
  DECOMPILE_BCS_TO_BAF
	REPLACE_TEXTUALLY CASE_INSENSITIVE ~"F_DRIZZT"~ ~"Drizzt"~
  COMPILE_BAF_TO_BCS
BUT_ONLY

// restore original drizzt dialog (backed up when installing core component)
COPY ~DRIZZTSAGA\DLG\DelayedStart\backup.dlg~ ~override\%_%drizzt.dlg~

// Assign an independent unique dialog to our Drizzt
// Along with the above changes, this will make the drizzt vs gnolls encounter play out exactly as vanilla
COPY_EXISTING ~F_DRIZZT.CRE~ ~override~
	WRITE_ASCII DIALOG "F_DRIZZT" #8
	SAY INITIAL_MEETING #-1		// don't use the initial lines from the gnoll encounter, they're not appropriate for the situation
BUT_ONLY

COPY_EXISTING ~%_%DRIZZT.CRE~ ~override~
	WRITE_BYTE 0x2c 30			// metal color
	WRITE_BYTE 0x2d 54			// minor color
	WRITE_BYTE 0x2e 66			// major color
	WRITE_BYTE 0x30 23			// leather color
	WRITE_BYTE 0x32 79			// hair color
BUT_ONLY

// dialog changes:

ADD_JOURNAL @10006 @10007

COMPILE EVALUATE_BUFFER ~DRIZZTSAGA/DLG/DelayedStart/F_DRIZZT.D~
						~DRIZZTSAGA/DLG/DelayedStart/F_STONGL.D~		// different LUA destination

// script changes/additions:

COPY_EXISTING ~%FishermansLake%.ARE~ ~override~
	READ_ASCII AREA_SCRIPT ~FishermansLake_AreaScript~	
BUT_ONLY

COPY_EXISTING ~%FishermansLake_AreaScript%.BCS~ ~override~
	R_B_B ~DRIZZTSAGA\Scripts\SNIP\eRegis.BAF~ ~.../DS-inlined/P5empty.baf~
BUT_ONLY

COPY_EXISTING ~F_DRIZZT.BCS~ ~override~	
	R_B_B ~DRIZZTSAGA\Scripts\SNIP\DelayedStart\F_DRIZZT_old.BAF~ ~DRIZZTSAGA\Scripts\SNIP\DelayedStart\F_DRIZZT_new.BAF~
BUT_ONLY
											
COPY_EXISTING ~%UlgothsBeard%.ARE~ ~override~
	READ_ASCII AREA_SCRIPT ~UlgothsBeard_AreaScript~
BUT_ONLY

COPY_EXISTING ~%DurlagsTower%.ARE~ ~override~
	READ_ASCII AREA_SCRIPT ~DurlagsTower_AreaScript~
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~%UlgothsBeard_AreaScript%.bcs~ THEN BEGIN
	EXTEND_BOTTOM ~%UlgothsBeard_AreaScript%.bcs~ ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/eARU000.BAF~
END ELSE BEGIN
	COPY ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/eARU000.BAF~ ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/%UlgothsBeard_AreaScript%.baf~
	COMPILE ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/%UlgothsBeard_AreaScript%.baf~
END

ACTION_IF FILE_EXISTS_IN_GAME ~%DurlagsTower_AreaScript%.bcs~ THEN BEGIN
	EXTEND_BOTTOM ~%DurlagsTower_AreaScript%.bcs~ ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/eARD000.BAF~
		EVALUATE_BUFFER
END ELSE BEGIN
	COPY ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/eARD000.BAF~ ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/%DurlagsTower_AreaScript%.baf~
	COMPILE EVALUATE_BUFFER ~DRIZZTSAGA/Scripts/SNIP/DelayedStart/%DurlagsTower_AreaScript%.baf~
END

// BGT transition - need to erase 2 additional journal entries:	
ACTION_IF GAME_IS ~BGT~ BEGIN
	COPY_EXISTING ~ARAM00.BCS~ ~override~
		DECOMPILE_BCS_TO_BAF
			REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
~DayNight(MIDNIGHT)~
~DayNight(MIDNIGHT)
    EraseJournalEntry(@10006)
    EraseJournalEntry(@10007)~
		COMPILE_BAF_TO_BCS
	BUT_ONLY
END

///////////////////////////////////////////////////////////////////////////////////////////////
//// Optional component: Creatures, Items and XP Rebalanced					  /////////////////
///////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @1510
GROUP @1509
DESIGNATED 4
REQUIRE_PREDICATE MOD_IS_INSTALLED "drizztsaga.tp2" "0" OR MOD_IS_INSTALLED "drizztsaga.tp2" "1" @1503

LAM ~DS_CREATURES_NERFING~

BEGIN @1511
GROUP @1509
DESIGNATED 5
REQUIRE_PREDICATE MOD_IS_INSTALLED "drizztsaga.tp2" "0" OR MOD_IS_INSTALLED "drizztsaga.tp2" "1" @1503

LAM ~DS_ITEMS_NERFING~

BEGIN @1512
GROUP @1509
DESIGNATED 6
REQUIRE_PREDICATE MOD_IS_INSTALLED "drizztsaga.tp2" "0" OR MOD_IS_INSTALLED "drizztsaga.tp2" "1" @1503

LAM ~DS_XP_NERFING~

///////////////////////////////////////////////////////
//// Optional Biffing /////////////////////////////////
///////////////////////////////////////////////////////

BEGIN @1513
DESIGNATED 1000
REQUIRE_PREDICATE MOD_IS_INSTALLED "drizztsaga.tp2" "0" OR MOD_IS_INSTALLED "drizztsaga.tp2" "1" @1503
REQUIRE_PREDICATE GAME_IS ~Tutu_TotSC BGT~ @1514

ACTION_IF GAME_IS ~Tutu_TotSC BGT~ THEN BEGIN
	PRINT @1505
	MAKE_BIFF DZTS-ARE BEGIN
		~override~ ~F_.*\.ARE~
		~override~ ~F_.*\.TIS~
		~override~ ~F_.*\.WED~
		~override~ ~F_.*\.MOS~
		~override~ ~F_.*HT\.BMP~
		~override~ ~F_.*LM\.BMP~
		~override~ ~F_.*SR\.BMP~
	END
END

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////  
// EOF
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////  
